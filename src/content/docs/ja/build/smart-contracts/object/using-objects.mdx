---
title: "オブジェクトの使用"
---

import { Aside } from '@astrojs/starlight/components';

[オブジェクトの作成](/ja/build/smart-contracts/object/creating-objects)でオブジェクトを作成した後,Moveのエントリー関数や構造体で使用したり,転送したり,生成時に作成した参照(ref)を使用して変更したりすることができます.以下では,MoveでObjectを利用,管理,操作するさまざまな方法を説明します.

## エントリー関数の引数としてのオブジェクトの使用

Moveの関数におけるオブジェクトの型は`Object<T>`で,`T`はオブジェクトが所有するリソースの型です.すべてのオブジェクトは,オブジェクトのメタデータを含む`ObjectCore`型を持っています.

オブジェクトパラメータを使用するには,ユーザーはオブジェクトのアドレスまたはオブジェクトへの参照を渡すことができます.実行時に,コントラクトはそのアドレスにオブジェクトが存在し,型Tのリソースを持っていることを関数を実行する前に検証します.

```move filename="Example.move"
module my_addr::object_playground {
  use aptos_framework::object::{Object, ObjectCore};

  struct MyAwesomeStruct has key {}

  /// This will fail if the object doesn't have MyAwesomeStruct stored
  entry fun do_something(object: Object<MyAwesomeStruct>) {
    // ...
  }

  /// All Objects have ObjectCore, so this will only fail if the
  /// address is not an object
  entry fun do_something_to_object_core(object: Object<ObjectCore>) {
    // ...
  }
}
```

エントリー関数のユーザーにリソースの型を指定させるには,以下のように汎用型`T`を使用します:

```move filename="Example.move"
module my_addr::object_playground {
  use aptos_framework::object::Object;

  /// This will fail if the object doesn't have the generic `T` stored
  entry fun do_something<T>(object: Object<T>) {
    // ...
  }
}
```

### オブジェクトの型

オブジェクトが所有する任意のリソースの型でオブジェクトを参照できます.便宜上,アドレスをオブジェクトに変換したり,リソースが利用可能な場合は`address_to_object`と`convert`を使用してオブジェクトを型間で変換したりできます:

```move filename="Example.move"
module my_addr::object_playground {
  use aptos_framework::object::{Self, Object, ObjectCore};

  struct MyAwesomeStruct has key {}

  fun convert_type(object: Object<ObjectCore>): Object<MyAwesomeStruct> {
    object::convert<ObjectCore, MyAwesomeStruct>(object)
  }

  fun address_to_type(object_address: address): Object<MyAwesomeStruct> {
    object::address_to_object<MyAwesomeStruct>(object_address)
  }
}
```

<Aside type="note">
  オブジェクトは,アカウント,オブジェクト,リソースアカウントを含む任意のアドレスによって所有できます.これにより,オブジェクト間の構成性と複雑な関係が可能になります.
</Aside>

## 構造体のフィールドの型としてのオブジェクトの使用

オブジェクトは,構造体で複雑な型を表現するのに役立ちます.例えば:

```move filename="Example.move"
module my_addr::object_playground {
  use aptos_framework::object::{Self, Object};
  use aptos_framework::fungible_asset::Metadata;
  use aptos_framework::primary_fungible_store;
  use std::signer;
  use std::option;
  use std::string::utf8;

  struct MyStruct has key {
    fungible_asset_object: Object<Metadata>
  }

  entry fun create_fungible_asset(creator: &signer) {
    let fa_obj_constructor_ref = &object::create_sticky_object(@my_addr);
    let fa_obj_signer = object::generate_signer(fa_obj_constructor_ref);
    let fa_obj_addr = signer::address_of(&fa_obj_signer);
    primary_fungible_store::create_primary_store_enabled_fungible_asset(
        fa_obj_constructor_ref, 
        option::none(),
        utf8(b"Asset name"),
        utf8(b"Asset symbol"),
        2,
        utf8(b"Icon uri"),
        utf8(b"Project uri")
    );
    move_to(creator, MyStruct {
      fungible_asset_object: object::address_to_object(fa_obj_addr)
    });
  }
}
```

## オブジェクトの所有者の確認

オブジェクトのコントラクトを作成する際,オブジェクトを変更する前に所有権を確認することが重要です.オブジェクトは任意のアドレスによって所有できるため,所有権の確認では,所有者がアカウント,リソースアカウント,または別のオブジェクトであるかを考慮する必要があります:

```move filename="Example.move"
module my_addr::object_playground {
  use std::signer;
  use aptos_framework::object::{Self, Object};

  // Not authorized!
  const E_NOT_AUTHORIZED: u64 = 1;

  fun check_owner_is_caller<T: key>(caller: &signer, object: Object<T>) {
    assert!(
      object::is_owner(object, signer::address_of(caller)),
      E_NOT_AUTHORIZED
    );
  }

  fun check_is_owner_of_object<T: key>(addr: address, object: Object<T>) {
    assert!(object::owner(object) == addr, E_NOT_AUTHORIZED);
  }

  fun check_is_nested_owner_of_object<T: key, U: key>(
    caller: &signer,
    outside_object: Object<T>,
    inside_object: Object<U>
  ) {
    // Ownership expected
    // Caller account -> Outside object -> inside object

    // Check outside object owns inside object
    let outside_address = object::object_address(&outside_object);
    assert!(object::owns(inside_object, outside_address), E_NOT_AUTHORIZED);

    // Check that the caller owns the outside object
    let caller_address = signer::address_of(caller);
    assert!(object::owns(outside_object, caller_address), E_NOT_AUTHORIZED);

    // Check that the caller owns the inside object (via the outside object)
    // This can skip the first two calls (and even more nested)
    assert!(object::owns(inside_object, caller_address), E_NOT_AUTHORIZED);
  }
}
```

## 所有権の移転

デフォルトでは,すべてのオブジェクトは転送可能です.一部のオブジェクトは,構築時に`ungated_transfer`を無効にするように設定されています(詳細については&quot;オブジェクトの構築&quot;を参照してください).

オブジェクトは以下のように転送できます:

```move filename="Example.move"
module my_addr::object_playground {
  use aptos_framework::object::{Self, Object};

  /// Transfer to another address, this can be an object or account
  fun transfer<T: key>(owner: &signer, object: Object<T>, destination: address) {
    object::transfer(owner, object, destination);
  }

  /// Transfer to another object
  fun transfer_to_object<T: key, U: key>(
    owner: &signer,
    object: Object<T>,
    destination: Object<U>
  ) {
    object::transfer_to_object(owner, object, destination);
  }
}
```

<Aside type="caution">
  `ungated_transfer`が**無効**な場合,すべての転送には`TransferRef`または`LinearTransferRef`によって与えられる特別な許可が必要です.
</Aside>

## イベント

デフォルトでは,オブジェクトは`TransferEvent`のみを持ち,これはオブジェクトが転送されるたびにトリガーされます.

オブジェクトは追加のイベントを持つように拡張できます.

オブジェクトのイベントハンドルを作成するには,以下の関数を使用できます:

```move filename="example.move"
module 0x1::object {
  /// Create a guid for the object, typically used for events
  public fun create_guid(object: &signer): guid::GUID {}

  /// Generate a new event handle.
  public fun new_event_handle<T: drop + store>(object: &signer): event::EventHandle<T> {}
}
```

生成されたイベントハンドルは,オブジェクトの`SignerRef`を持っている限り,オブジェクトに転送できます.例えば:

```move filename="example.move"
module 0x42::example {
  use aptos_framework::event;
  use aptos_framework::fungible_asset::Metadata;
  use aptos_framework::object::{Self, Object};

  #[resource_group_member(group = aptos_framework::object::ObjectGroup)]  
  struct LiquidityPoolResourceGroup has key {
    pool: LiquidityPool,
    event_store: LiquidityPoolEventStore,
  }

  struct LiquidityPool has store {
    metadata_token_a: Object<Metadata>,
    metadata_token_b: Object<Metadata>,
    reserves_a: u128,
    reserves_b: u128,
  }

  struct LiquidityPoolEventStore has store {
    create_events: event::EventHandle<CreateLiquidityPoolEvent>,
  }

  #[event]
  struct CreateLiquidityPoolEvent has store, drop {
    token_a: address,
    token_b: address,
    reserves_a: u128,
    reserves_b: u128,
  }

  public entry fun create_liquidity_pool_with_events(
        account_signer: &signer,
        metadata_token_a: Object<Metadata>,
        metadata_token_b: Object<Metadata>,
        reserves_a: u128,
        reserves_b: u128
  ) {
    let liquidity_pool_constructor_ref = &object::create_object_from_account(
      account_signer
    );
    let liquidity_pool_signer = &object::generate_signer(
      liquidity_pool_constructor_ref
    );
    let event_handle = object::new_event_handle<CreateLiquidityPoolEvent>(
      liquidity_pool_signer
    );

    event::emit_event<CreateLiquidityPoolEvent>(&mut event_handle, CreateLiquidityPoolEvent {
      token_a: object::object_address(&metadata_token_a),
      token_b: object::object_address(&metadata_token_b),
      reserves_a,
      reserves_b,
    });

    move_to(liquidity_pool_signer, LiquidityPoolResourceGroup {
      pool: LiquidityPool {
        metadata_token_a,
        metadata_token_b,
        reserves_a,
        reserves_b
      },
      event_store: LiquidityPoolEventStore {
        create_events: event_handle
      }
    });
  }
}
```

## 作成後のオブジェクトの変更

一般的に,オブジェクトは作成時に生成された`Refs`でのみ変更できます.利用可能な`Refs`,その生成方法,使用方法の詳細については,[オブジェクトの作成と設定](/ja/build/smart-contracts/object/creating-objects)を参照してください.これは,オブジェクトに追加のリソースを追加したり,削除したり,拡張したりする方法です.

## サンプルコントラクト

以下は,オブジェクトを使用する実際のコードの例です:

- [デジタルアセットマーケットプレイスの例](https://github.com/aptos-labs/aptos-core/tree/main/aptos-move/move-examples/marketplace)
- [デジタルアセットの例](https://github.com/aptos-labs/aptos-core/tree/main/aptos-move/move-examples/token_objects)
- [代替性資産の例](https://github.com/aptos-labs/aptos-core/tree/main/aptos-move/move-examples/fungible_asset)
